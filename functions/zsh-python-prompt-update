#
# zsh-python-prompt-update
#


function _zsh_python_prompt_pyenv(){
    #/usr/local/Cellar/python/2.7.8_1/Frameworks/Python.framework/Versions/2.7/bin/python2.7
    #~/.pyenv/versions/2.7.8/bin/python2.7
    #~/.pyenv/versions/pyenv_t/bin/python

    local python_path pyver pytype now pyver_file \
        prompt prompt_cache updated_at pyenv_expire_sec pyenv_symbol

    python_path=$1

    zstyle -s ':zsh-python-prompt:' prompt prompt
    zstyle -s ':zsh-python-prompt:' prompt_cache prompt_cache
    zstyle -s ':zsh-python-prompt:' updated_at updated_at
    zstyle -s ':zsh-python-prompt:pyenv:' expire_sec pyenv_expire_sec
    zstyle -s ':zsh-python-prompt:pyenv:' pyver_file pre_pyver_file

    pyver_file=$(_zsh_python_prompt_get_pyver_file)
    now=$(date +%s)

    if [[ -n $prompt_cache
        && $pyver_file == $pre_pyver_file
        && $now -lt $(($updated_at + $pyenv_expire_sec)) ]]; then
        # Don't update
        zstyle ':zsh-python-prompt:' prompt "$prompt_cache"
        return 1
    fi

    zstyle ':zsh-python-prompt:' updated_at "$now"
    zstyle ':zsh-python-prompt:pyenv:' pyver_file "$pyver_file"

    python_path=$(python -c "import sys,os; print(os.path.realpath(sys.executable))")
    pyver=$(_zsh_python_prompt_get_pyver)

    if [[ $python_path == '/usr/bin/python' ]]; then
        pytype='system'
    elif [[ $python_path =~ '^/usr/local' ]]; then
        pytype='local'
    elif [[ $python_path =~ '/.pyenv/versions/' ]]; then
        pytype=$(echo "$python_path" | sed -e 's%.*\.pyenv/versions/\([^/]*\)/.*%\1%')
        if [[ $pytype == $pyver ]]; then
            pytype=
        fi
    else
        pytype=$python_path
    fi

    zstyle -s ':zsh-python-prompt:pyenv:' symbol pyenv_symbol

    prompt_cache="$pyenv_symbol$pytype@$pyver"

    zstyle ':zsh-python-prompt:' prompt_cache "$prompt_cache"
    zstyle ':zsh-python-prompt:' prompt "*$prompt_cache"
}

function _zsh_python_prompt_pybrew(){
    local python_path pyver pytype

    python_path=$1
    pyver=$(_zsh_python_prompt_get_pyver)

    if [ -n "$VIRTUAL_ENV" ]; then
        pytype=$(basename "$VIRTUAL_ENV")
    else
        pytype=$(echo "$python_path" | sed 's%.*Python-\([^/]*\)/.*%\1%' | tr -d '.')
    fi
    pytype="!!$pytype"

    zstyle ':zsh-python-prompt:' prompt "$pytype@$pyver"
}

function _zsh_python_prompt_normal(){
    local python_path pyver pytype

    python_path=$1
    pyver=$(_zsh_python_prompt_get_pyver)

    if [[ $python_path == '/usr/bin/python' ]]; then
        pytype='system'
    elif [[ $python_path =~ '^/usr/local' ]]; then
        pytype='local'
    else
        if [ -n "$VIRTUAL_ENV" ]; then
            pytype=$(basename "$VIRTUAL_ENV")
        else
            pytype=$python_path
        fi
        pytype="!!$pytype"
    fi

    zstyle ':zsh-python-prompt:' prompt "$pytype@$pyver"
}

function _zsh_python_prompt_get_pyver(){
    python -V 2>&1 | sed -e 's/Python //'
}

function _zsh_python_prompt_get_pyver_file() {
    #.python-version
    local pyver_file
    local curpath="$PWD"
    while [ "$curpath" ]; do
        pyver_file="$curpath/.python-version"
        if [[ -e "$pyver_file" ]]; then
            echo "$pyver_file"
            return
        fi
        curpath=${curpath%/*}
    done
    return 1
}

function zsh-python-prompt-update(){
    local python_path prompt

    python_path=$(which python 2> /dev/null)
    if [[ -z $python_path ]]; then
        zstyle ':zsh-python-prompt:' prompt 'none'
        zstyle ':zsh-python-prompt:' prompt_cache ''
        return
    fi

    if [[ $python_path =~ '/.pyenv/shims/python'
        || $python_path =~ '/.pyenv/versions/' ]]; then
        # pyenv
        _zsh_python_prompt_pyenv "$python_path"
    else
        if [[ $python_path =~ '/.pythonbrew/' ]]; then
            # pythohbrew
            _zsh_python_prompt_pybrew "$python_path"
        else
            # normal
            _zsh_python_prompt_normal "$python_path"
        fi
        zstyle ':zsh-python-prompt:' prompt_cache ''
    fi
}

zsh-python-prompt-update "$@"

# vim: ft=zsh
